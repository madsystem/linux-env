# $Id: PKGBUILD 154170 2015-12-22 09:55:49Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Dag Odenhall <dag.odenhall@gmail.com>
# Contributor: Grigorios Bouzakis <grbzks@gmail.com>

pkgname=dwm
pkgver=6.1
pkgrel=2
pkgdesc="A dynamic window manager for X"
url="http://dwm.suckless.org"
arch=('i686' 'x86_64')
license=('MIT')
options=(zipman)
depends=('libx11' 'libxinerama' 'libxft' 'freetype2' 'st')
install=dwm.install
source=(dwm-$pkgver.tar.gz
    dwm-6.1-systray.diff
    dwm-6.1-pertag.diff
    dwm-6.1-better-borders.diff
    dwm-6.1-single-tagset.diff
    dwm-6.1-my.diff
	dwm.desktop)
md5sums=('f0b6b1093b7207f89c2a90b848c008ec'
         '964ab536b175afbd3529aba596c8fa68'
         '3126263695676ffa7ab7d90e49bd77cb'
         'c3417a55ddc4d0b9a7e3641ef16484a0'
         'f5aded5492ae95b22a0793414412d37d'
         '3b03180aa5b768ead7188ea9eca7dd6f'
         '939f403a71b6e85261d09fc3412269ee')

prepare() {
  cd $srcdir/$pkgname-$pkgver

  patch -p1 -i ../dwm-6.1-systray.diff
  patch -p1 -i ../dwm-6.1-pertag.diff
  patch -p1 -i ../dwm-6.1-better-borders.diff
  patch -p1 -i ../dwm-6.1-single-tagset.diff
  cp config.def.h config.h

  patch -p1 -i ../dwm-6.1-my.diff
}

build() {
  cd $srcdir/$pkgname-$pkgver
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11 FREETYPEINC=/usr/include/freetype2
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make PREFIX=/usr DESTDIR=$pkgdir install
  install -m644 -D LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
  install -m644 -D README $pkgdir/usr/share/doc/$pkgname/README
  install -m644 -D $srcdir/dwm.desktop $pkgdir/usr/share/xsessions/dwm.desktop
}
